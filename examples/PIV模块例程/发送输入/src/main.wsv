<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <注释 = "系统设置、防火墙、杀毒软件、游戏、输入法都可能拦截这个API.">

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()

        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"362, 250\"" 标题 = "发送输入测试">
{
    变量 编辑框1 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"67, 23, 208, 96\"" 是否允许多行 = 真>
    变量 按钮1 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"89, 151, 67, 27\"" 标题 = "鼠标输入">
    变量 按钮2 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"189, 151, 67, 27\"" 标题 = "键盘输入">

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件" 折叠>
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮1)
        {
            变量 输入数组 <类型 = 输入结构数组类>
            变量 输入信息 <类型 = 输入结构类>
            输入信息.类型 = 0
            输入信息.鼠标.dx = 100
            输入信息.鼠标.dy = 20
            输入信息.鼠标.dwFlags = 鼠标事件常量类.鼠标移动
            输入数组.加入成员 (输入信息)

            输入信息.鼠标.dx = 40
            输入信息.鼠标.dy = 80
            输入信息.鼠标.dwFlags = 鼠标事件常量类.鼠标移动
            输入数组.加入成员 (输入信息)

            输入信息.鼠标.dx = 0
            输入信息.鼠标.dy = 0
            输入信息.鼠标.dwFlags = 鼠标事件常量类.按下右键
            输入数组.加入成员 (输入信息)

            输入信息.鼠标.dwFlags = 鼠标事件常量类.释放右键
            输入数组.加入成员 (输入信息)

            键鼠输入.发送输入 (输入数组)

        }
        否则 (来源对象 == 按钮2)
        {
            变量 输入数组 <类型 = 输入结构数组类>
            变量 输入信息 <类型 = 输入结构类>
            // 键盘输入一般每个按键两个成员,一个按下按键,一个释放按键
            输入信息.类型 = 1
            输入信息.键盘.wVk = (短整数)按键码.G键
            输入信息.键盘.dwFlags = 0
            输入数组.加入成员 (输入信息)

            输入信息.键盘.wVk = (短整数)按键码.G键
            输入信息.键盘.dwFlags = 2
            输入数组.加入成员 (输入信息)

            输入信息.键盘.wVk = (短整数)按键码.J键
            输入信息.键盘.dwFlags = 0
            输入数组.加入成员 (输入信息)

            输入信息.键盘.wVk = (短整数)按键码.J键
            输入信息.键盘.dwFlags = 2
            输入数组.加入成员 (输入信息)

            输入信息.键盘.wVk = 0
            输入信息.键盘.wScan = (短整数)'火'  // 直接输入UNICODE字符
            输入信息.键盘.dwFlags = 4
            输入数组.加入成员 (输入信息)

            输入信息.键盘.wScan = (短整数)'火'
            输入信息.键盘.dwFlags = 位或 (4, 2)
            输入数组.加入成员 (输入信息)

            编辑框1.获取焦点 ()
            键鼠输入.发送输入 (输入数组)

        }
        返回 (0)
    }
}
