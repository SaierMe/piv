<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"665, 330\"" 标题 = "剪贴板操作">
{
    变量 按钮1 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"15, 30, 120, 32\"" 标题 = "复制文件">
    变量 按钮2 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"15, 80, 120, 32\"" 标题 = "剪切文件">
    变量 按钮6 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"509, 80, 140, 32\"" 标题 = "画板到剪贴板">
    变量 按钮5 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"509, 30, 140, 32\"" 标题 = "图片到剪贴板">
    变量 按钮3 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"15, 130, 120, 32\"" 标题 = "粘贴文件">
    变量 按钮4 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"15, 180, 120, 32\"" 标题 = "取剪贴板文件">
    变量 按钮7 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"509, 130, 140, 32\"" 标题 = "粘贴图像到画板">
    变量 按钮8 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\npos = \"509, 180, 140, 32\"" 标题 = "保存剪贴板图像">
    变量 画板1 <类型 = 画板 隐藏值属性 = "0" @视窗.布局 = "id = 109\r\npos = \"146, 27, 350, 280\"" 画板背景色 = 0xC0C0C0
            底图方式 = 缩放图片>
    变量 按钮9 <类型 = 按钮 隐藏值属性 = "0" @视窗.布局 = "id = 110\r\npos = \"509, 230, 140, 32\"" 标题 = "打开文件到画板">

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮1)
        {
            变量 文件列表 <类型 = 文本数组类>
            如果 (通用文件对话框.打开选择多个文件 (文件列表) == 0)
            {
                信息框 ("未选择文件。")
                返回 (0)
            }
            如果 (剪贴板类.复制多个文件到剪贴板 (文件列表) == 真)
            {
                信息框 ("复制文件到剪贴板成功，你可以在文件资源管理器中粘贴复制文件。")
            }
            否则
            {
                信息框 ("复制文件到剪贴板失败。")

            }
        }
        否则 (来源对象 == 按钮2)
        {
            变量 文件列表 <类型 = 文本数组类>
            如果 (通用文件对话框.打开选择多个文件 (文件列表) == 0)
            {
                信息框 ("未选择文件。")
                返回 (0)
            }
            如果 (剪贴板类.剪切多个文件到剪贴板 (文件列表) == 真)
            {
                信息框 ("剪切文件到剪贴板成功，你可以在文件资源管理器中粘贴移动文件。")
            }
            否则
            {
                信息框 ("剪切文件到剪贴板失败。")
            }

        }
        否则 (来源对象 == 按钮3)
        {
            变量 目标位置 <类型 = 文本型>
            目标位置 = 浏览文件夹 ("请选择文件的保存位置")
            如果 (文本是否为空 (目标位置) == 真)
            {
                信息框 ("未选择目标位置。")
                返回 (0)
            }
            如果 (剪贴板类.粘贴文件自剪贴板 (目标位置, 假) == 真)
            {
                信息框 ("粘贴文件成功。")
            }
            否则
            {
                信息框 ("粘贴文件失败。")
            }

        }
        否则 (来源对象 == 按钮4)
        {
            变量 返回值 <类型 = 整数>
            变量 文件列表 <类型 = 文本数组类>
            变量 文件名列表 <类型 = 文本型>
            返回值 = 剪贴板类.取剪贴板文件 (文件列表)
            文件列表.枚举循环 ()
            {
                文件名列表 = 文件名列表 + 文件列表.取枚举值 () + "\r\n"
            }
            如果 (返回值 == 0)
            {
                信息框 ("剪贴板中没有已复制或剪切的文件数据。")
            }
            否则 (返回值 == 1)
            {
                信息框 ("找到待复制的文件：\r\n" + 文件名列表)
            }
            否则 (返回值 == 2)
            {
                信息框 ("找到待剪切的文件：\r\n" + 文件名列表)

            }
        }
        否则 (来源对象 == 按钮5)
        {
            变量 图像文件 <类型 = 文本型>
            如果 (通用文件对话框.打开文件 (图像文件, "所有支持的图像文件|*.bmp;*.jpg;*.png;*.ico;*.gif;*.emf;*.wmf;*.tif") == 真)
            {
                如果 (剪贴板类.置剪贴板图像文件 (图像文件) == 真)
                {
                    信息框 ("复制图像成功，你现在可以在画图、QQ等软件粘贴图像。")
                }
                否则
                {
                    信息框 ("复制图像到剪贴板失败！")
                }

            }

        }
        否则 (来源对象 == 按钮6)
        {
            变量 位图对象 <类型 = 位图对象类>
            画板1.取图片 (位图对象)
            如果 (剪贴板类.置剪贴板图像 (位图对象) == 真)
            {
                信息框 ("复制图像成功，你现在可以在画图、QQ等软件粘贴图像。\r\n\r\n注意：画板中的图像经过缩放，已经不是原图。")
            }
            否则
            {
                信息框 ("复制画板的图像到剪贴板失败！")

            }
        }
        否则 (来源对象 == 按钮7)
        {
            变量 位图对象 <类型 = 位图对象类>
            如果 (剪贴板类.取剪贴板图像 (位图对象) == 假)
            {
                信息框 ("剪贴板中没有位图数据。")
                返回 (0)
            }
            画板1.画图片 (位图对象, 0, 0, 350, 280)

        }
        否则 (来源对象 == 按钮8)
        {
            变量 文件名 <类型 = 文本型>
            通用文件对话框.保存文件 (文件名, "位图文件(*.bmp)|*.bmp", , , , , "bmp")
            如果 (文本是否为空 (文件名) == 真)
            {
                信息框 ("未选择保存的文件名。")
                返回 (0)
            }
            如果 (剪贴板类.取剪贴板图像到文件 (文件名, 假) == 真)
            {
                信息框 ("保存位图成功。")
            }
            否则
            {
                信息框 ("保存剪贴板中的位图数据失败！\r\n\r\n可能是剪贴板中没有位图数据。")

            }

        }
        否则 (来源对象 == 按钮9)
        {
            变量 图像文件 <类型 = 文本型>
            变量 位图对象 <类型 = 位图对象类>
            变量 位图 <类型 = 位图资源>
            如果 (通用文件对话框.打开文件 (图像文件, "所有支持的图像文件|*.bmp;*.jpg;*.png;*.ico;*.gif;*.emf;*.wmf;*.tif") == 真)
            {
                位图对象 = 载入位图文件 (图像文件)
                如果 (位图对象.为空 () == 真)
                {
                    信息框 ("打开的文件不是图像格式。")
                    返回 (0)
                }
                画板1.画图片 (位图对象, 0, 0, 350, 280)

            }

        }
        返回 (0)
    }
}
