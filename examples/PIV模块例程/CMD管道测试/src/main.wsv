<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数 折叠>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"600, 380\"" 标题 = "命令行管道">
{
    变量 编辑框_屏显 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"20, 20, 560, 300\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条 输入方式 = 只读方式>
    变量 编辑框_命令 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"20, 333, 325, 32\"">
    变量 按钮_发送命令 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"353, 333, 60, 32\"" 标题 = "执行">
    变量 按钮_管道开关 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"482, 333, 100, 32\"" 标题 = "关闭管道">
    变量 按钮_终止 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"417, 333, 60, 32\"" 标题 = "终止">
    变量 主线程处理 <类型 = 高级主线程处理器>
    变量 匿名管道 <类型 = 管道命令行类>

    方法 我的主窗口_创建完毕 <接收事件 类型 = 整数 注释 = "当本组件及其中所有子组件均被创建完毕后发送此事件." 折叠>
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        编辑框_命令.内容 = "ping www.baidu.com"
        如果 (匿名管道.是否已启动 == 真)
        {
            按钮_发送命令.禁止 = 假
            按钮_管道开关.标题 = "关闭管道"
        }
        否则
        {
            按钮_发送命令.禁止 = 真
            按钮_管道开关.标题 = "开启管道"

        }
        返回 (0)
    }

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件" 折叠>
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_发送命令)
        {
            如果 (匿名管道.是否空闲)
            {
                匿名管道.执行命令 (编辑框_命令.内容)
                编辑框_命令.内容 = ""
                按钮_发送命令.禁止 = 真

            }

        }
        否则 (来源对象 == 按钮_管道开关)
        {
            如果 (匿名管道.是否已启动 == 真)
            {
                匿名管道.关闭命令行 ()
                按钮_发送命令.禁止 = 真
                按钮_管道开关.标题 = "开启管道"
            }
            否则
            {
                匿名管道.启动命令行 ()
                按钮_发送命令.禁止 = 假
                按钮_管道开关.标题 = "关闭管道"

            }
        }
        否则 (来源对象 == 按钮_终止)
        {
            匿名管道.终止命令 ()

        }
        返回 (0)
    }

    方法 管道命令行类_收到输出信息 <接收事件 类型 = 整数 注释 = "启动命令行提示符或执行命令后,在此返回命令的输出信息." 返回值注释 = "此返回值无意义">
    参数 来源对象 <类型 = 管道命令行类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 输出信息 <类型 = 字节集类 注释 = "  命令行提示符打印在屏幕上的文本,请根据命令行的编码" 注释 = "使用\"多字节到文本\"或\"UTF8到文本\"等命令转换到文本型.">
    {
        如果 (来源对象 == 匿名管道)
        {
            主线程处理.发送通知 (1, , , 文本到对象 (多字节到文本 (输出信息)))
            如果 (来源对象.是否空闲)
            {
                按钮_发送命令.禁止 = 假

            }

        }
        返回 (0)
    }

    方法 高级主线程处理器_接收到通知 <接收事件 类型 = 整数 注释 = "在其它线程中调用本对象的\"发送通知\"/\"投递通知\"方法后,本事件将被触发."
            注释 = "本事件的接收方法将始终在程序的UI主线程中执行." 注释 = "本事件中的参数值为调用\"发送通知\"/\"投递通知\"方法时所提供的对应参数值."
            返回值注释 = "如果本事件由\"发送通知\"方法调用触发,事件处理方法的返回值将返回到\"发送通知\"方法的调用方.">
    参数 来源对象 <类型 = 高级主线程处理器 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 通知码 <类型 = 变整数>
    参数 参数1 <类型 = 变整数>
    参数 参数2 <类型 = 变整数>
    参数 参数对象 <类型 = 对象包装类 注释 = "包装中为发送/投递通知时所提供对象的复制对象,如果未提供则为空(\"参数对象.是否为空\"方法返回真).">
    {
        如果 (来源对象 == 主线程处理)
        {
            编辑框_屏显.加入文本 (对象到文本 (参数对象.取对象 ()))
            处理事件 ()
        }
        返回 (0)
    }
}
