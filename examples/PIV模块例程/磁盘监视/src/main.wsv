<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数 折叠>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"600, 400\"" 标题 = "磁盘监视" 最大化按钮 = 真
        窗口边框 = 普通可调边框>
{
    变量 按钮1 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"506, 20, 80, 32\"" 标题 = "开始监视">
    变量 按钮2 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"506, 64, 80, 32\"" 标题 = "停止监视" 禁止 = 真>
    变量 超级列表框1 <类型 = 超级列表框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"20, 15, 472, 365\"" "类型" = 报表列表框
            报表列 = "0\n-1\n0\n50\n时间\n0\n-1\n0\n50\n类型\n0\n-1\n0\n50\n理由\n0\n-1\n0\n50\n名称\n0\n-1\n0\n50\n路径"
            虚拟化数据 = 真 整行选择 = 真>
    变量 ""
    变量 磁盘监视 <类型 = 磁盘监视类>
    变量 文件更改数组 <类型 = 文件更改数组类>
    变量 互斥锁 <类型 = 互斥对象类>

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件" 折叠>
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮1)
        {
            变量 盘符数组 <类型 = 字符数组类>
            磁盘监视类.枚举所有盘符 (盘符数组, 真)
            盘符数组.枚举循环 ()
            {
                如果 (盘符数组.取枚举值 () == 'C')
                {
                    变量 监视目录组 <类型 = 文本数组类>
                    监视目录组.加入成员 (取特定目录 (特定目录类型.临时文件目录))
                    监视目录组.加入成员 (取特定目录 (特定目录类型.系统桌面))
                    // 磁盘监视.开始监视 ('C')
                }
                否则
                {
                    磁盘监视.开始监视 (盘符数组.取枚举值 ())
                }

            }
            按钮1.禁止 = 真
            按钮2.禁止 = 假
        }
        否则 (来源对象 == 按钮2)
        {
            磁盘监视.全部停止 ()
            按钮1.禁止 = 假
            按钮2.禁止 = 真

        }
        返回 (0)
    }

    方法 我的主窗口_尺寸被改变 <接收事件 类型 = 整数 注释 = "当本组件的尺寸被改变后发送此事件" 折叠>
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        按钮1.左边 = 本对象.宽度 - 110
        按钮2.左边 = 本对象.宽度 - 110
        超级列表框1.宽度 = 本对象.宽度 - 140
        超级列表框1.高度 = 本对象.高度 - 70
        超级列表框1.置列宽 (0, 取整 (超级列表框1.宽度 * 0.135))
        超级列表框1.置列宽 (1, 取整 (超级列表框1.宽度 * 0.08))
        超级列表框1.置列宽 (2, 取整 (超级列表框1.宽度 * 0.08))
        超级列表框1.置列宽 (3, 取整 (超级列表框1.宽度 * 0.28))
        超级列表框1.置列宽 (4, 取整 (超级列表框1.宽度 * 0.42))
        返回 (0)
    }

    方法 超级列表框_虚拟表项填充 <接收事件 类型 = 整数 注释 = "  在超级列表框即将绘制某虚拟项目之前被发送,用户可以修改所提供参数中的\"项目文本\"、\"表项数值\"、"
            注释 = "\"图片索引\"和\"检查框状态\"成员值以修改在绘制时所实际使用的表项数据." 注释 = "  本事件仅当\"虚拟化数据\"属性为真时才会发送." 折叠>
    参数 来源对象 <类型 = 超级列表框 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 表项参数 <类型 = 超级列表框表项填充 注释 = "  用户可以修改本参数中的\"项目文本\"、\"表项数值\"、\"图片索引\"和\"检查框状态\"成员值以修改在绘制"
            注释 = "时所实际使用的表项数据." 注释 = "  注意: 如果修改了本参数中的\"项目文本\"、\"表项数值\"、\"图片索引\"和\"检查框状态\"成员值,将会对"
            注释 = "当前表项的当前列及其后所有列均造成影响.">
    {
        如果 (来源对象 == 超级列表框1)
        {
            分支判断 (表项参数.列索引)
            {
                分支 (0)
                {
                    表项参数.项目文本 = 文件更改数组.取成员 (表项参数.表项索引).时间
                }
                分支 (1)
                {
                    表项参数.项目文本 = 选择 (文件更改数组.取成员 (表项参数.表项索引).为目录, "目录", "文件")
                }
                分支 (2)
                {
                    表项参数.项目文本 = 文件更改数组.取成员 (表项参数.表项索引).更改理由
                }
                分支 (3)
                {
                    表项参数.项目文本 = 文件更改数组.取成员 (表项参数.表项索引).文件名称
                }
                分支 (4)
                {
                    表项参数.项目文本 = 文件更改数组.取成员 (表项参数.表项索引).路径
                }
            }
        }
        返回 (0)
    }

    # ---

    方法 磁盘监视类_初始化完毕 <接收事件 类型 = 整数 注释 = "USN日志初始化完毕后将发送一次本事件,表示将开始监视文件的更改." 折叠>
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    {
        如果 (来源对象 == 磁盘监视)
        {
            调试输出 (盘符, "初始化成功")
        }
        返回 (0)
    }

    方法 磁盘监视类_文件被创建 <接收事件 类型 = 整数 注释 = "监视到创建了新文件或目录">
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    参数 文件名称 <类型 = 文本型 注释 = "触发更改的文件或目录名称">
    参数 当前路径 <类型 = 文本型 注释 = "本次更改的所在路径,以路径符结尾.">
    参数 时间戳 <类型 = 文件时间结构 注释 = "本次更改的文件时间戳(非Unix时间戳)">
    参数 属性值 <类型 = 整数 注释 = "文件或目录的属性值,为\"文件属性\"常量的组合值;" 注释 = "可用\"位与\"计算来判断是否具有某个属性;"
            注释 = "如: 为子目录 = 位与 (属性值, 文件属性.子目录) != 0">
    {
        如果 (来源对象 == 磁盘监视)
        {
            互斥锁.互斥自动锁 ()  // 多线程下需要加锁
            文件更改数组.构造成员 (时间戳.到时间文本 ("%T"), "创建", 文件名称, 当前路径, 位与 (属性值, 文件属性.子目录) != 0)
            提交到主线程 ()
            {
                超级列表框1.虚拟表项数 = 文件更改数组.取成员数 ()

            }
        }
        返回 (0)
    }

    方法 磁盘监视类_文件被删除 <接收事件 类型 = 整数 注释 = "监视到某个文件或目录被删除">
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    参数 文件名称 <类型 = 文本型 注释 = "触发更改的文件或目录名称">
    参数 当前路径 <类型 = 文本型 注释 = "本次更改的所在路径,以路径符结尾.">
    参数 时间戳 <类型 = 文件时间结构 注释 = "本次更改的文件时间戳(非Unix时间戳)">
    参数 属性值 <类型 = 整数 注释 = "文件或目录的属性值,为\"文件属性\"常量的组合值;" 注释 = "可用\"位与\"计算来判断是否具有某个属性;"
            注释 = "如: 为子目录 = 位与 (属性值, 文件属性.子目录) != 0">
    {
        如果 (来源对象 == 磁盘监视)
        {
            互斥锁.互斥自动锁 ()
            文件更改数组.构造成员 (时间戳.到时间文本 ("%T"), "删除", 文件名称, 当前路径, 位与 (属性值, 文件属性.子目录) != 0)
            提交到主线程 ()
            {
                超级列表框1.虚拟表项数 = 文件更改数组.取成员数 ()
            }

        }
        返回 (0)
    }

    方法 磁盘监视类_文件内容改变 <接收事件 类型 = 整数 注释 = "监视到某个文件的内容数据被修改">
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    参数 文件名称 <类型 = 文本型 注释 = "触发更改的文件或目录名称">
    参数 当前路径 <类型 = 文本型 注释 = "本次更改的所在路径,以路径符结尾.">
    参数 更改原因 <类型 = 整数 注释 = "文件或目录被更改的原因,请与\"USN更改原因\"的常量进行位与判断.">
    参数 时间戳 <类型 = 文件时间结构 注释 = "本次更改的文件时间戳(非Unix时间戳)">
    参数 属性值 <类型 = 整数 注释 = "文件或目录的属性值,为\"文件属性\"常量的组合值;" 注释 = "可用\"位与\"计算来判断是否具有某个属性;"
            注释 = "如: 为子目录 = 位与 (属性值, 文件属性.子目录) != 0">
    {
        如果 (来源对象 == 磁盘监视)
        {
            互斥锁.互斥自动锁 ()
            文件更改数组.构造成员 (时间戳.到时间文本 ("%T"), "修改", 文件名称, 当前路径, 位与 (属性值, 文件属性.子目录) != 0)
            提交到主线程 ()
            {
                超级列表框1.虚拟表项数 = 文件更改数组.取成员数 ()
            }

        }
        返回 (0)
    }

    方法 磁盘监视类_文件被重命名 <接收事件 类型 = 整数 注释 = "监视到某个文件或目录被重命名">
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    参数 旧名称 <类型 = 文本型 注释 = "重命名前的旧名称">
    参数 新名称 <类型 = 文本型 注释 = "重命名后的新名称">
    参数 当前路径 <类型 = 文本型 注释 = "本次更改的所在路径,以路径符结尾.">
    参数 时间戳 <类型 = 文件时间结构 注释 = "本次更改的文件时间戳(非Unix时间戳)">
    参数 属性值 <类型 = 整数 注释 = "文件或目录的属性值,为\"文件属性\"常量的组合值;" 注释 = "可用\"位与\"计算来判断是否具有某个属性;"
            注释 = "如: 为子目录 = 位与 (属性值, 文件属性.子目录) != 0">
    {
        如果 (来源对象 == 磁盘监视)
        {
            互斥锁.互斥自动锁 ()
            文件更改数组.构造成员 (时间戳.到时间文本 ("%T"), "重命名", 旧名称 + " >> " + 新名称, 当前路径, 位与 (属性值, 文件属性.子目录) != 0)
            提交到主线程 ()
            {
                超级列表框1.虚拟表项数 = 文件更改数组.取成员数 ()
            }

        }
        返回 (0)
    }

    方法 磁盘监视类_被删到回收站 <接收事件 类型 = 整数 注释 = "监视到某个文件或目录被删除到回收站">
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    参数 文件名称 <类型 = 文本型 注释 = "被删除到回收站之前的原名称">
    参数 原路径 <类型 = 文本型 注释 = "被删除文件的原始路径,以路径符结尾.">
    参数 回收站路径 <类型 = 文本型 注释 = "删除到回收站后的完整路径(文件被重命名为回收站内部使用的名称)">
    参数 时间戳 <类型 = 文件时间结构 注释 = "本次更改的文件时间戳(非Unix时间戳)">
    参数 属性值 <类型 = 整数 注释 = "文件或目录的属性值,为\"文件属性\"常量的组合值;" 注释 = "可用\"位与\"计算来判断是否具有某个属性;"
            注释 = "如: 为子目录 = 位与 (属性值, 文件属性.子目录) != 0">
    {
        如果 (来源对象 == 磁盘监视)
        {
            互斥锁.互斥自动锁 ()
            文件更改数组.构造成员 (时间戳.到时间文本 ("%T"), "回收站", 文件名称, 原路径 + " >> " + 回收站路径, 位与 (属性值, 文件属性.子目录) != 0)
            提交到主线程 ()
            {
                超级列表框1.虚拟表项数 = 文件更改数组.取成员数 ()
            }


        }
        返回 (0)
    }

    方法 磁盘监视类_文件被移动 <接收事件 类型 = 整数 注释 = "监视到某个文件或目录被移动到同盘的另一个位置" 注释 = "注: 移动到其他盘是触发\"文件被删除\"事件.">
    参数 来源对象 <类型 = 磁盘监视类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    参数 盘符 <类型 = 字符 注释 = "当前监视的盘符">
    参数 旧名称 <类型 = 文本型 注释 = "文件移动前的旧名称">
    参数 新名称 <类型 = 文本型 注释 = "文件移动后的新名称">
    参数 原路径 <类型 = 文本型 注释 = "文件移动前的原路径,以路径符结尾.">
    参数 新路径 <类型 = 文本型 注释 = "文件移动后的新路径,以路径符结尾.">
    参数 时间戳 <类型 = 文件时间结构 注释 = "本次更改的文件时间戳(非Unix时间戳)">
    参数 属性值 <类型 = 整数 注释 = "文件或目录的属性值,为\"文件属性\"常量的组合值;" 注释 = "可用\"位与\"计算来判断是否具有某个属性;"
            注释 = "如: 为子目录 = 位与 (属性值, 文件属性.子目录) != 0">
    {
        如果 (来源对象 == 磁盘监视)
        {
            互斥锁.互斥自动锁 ()
            文件更改数组.构造成员 (时间戳.到时间文本 ("%T"), "移动", 旧名称 + " >> " + 新名称, 原路径 + " >> " + 新路径, 位与 (属性值, 文件属性.子目录) != 0)
            提交到主线程 ()
            {
                超级列表框1.虚拟表项数 = 文件更改数组.取成员数 ()
            }


        }
        返回 (0)
    }
}

类 文件更改日志 <公开 注释 = "用于超级列表框的虚拟化数据" 折叠>
{
    变量 时间 <公开 类型 = 文本型>
    变量 更改理由 <公开 类型 = 文本型>
    变量 文件名称 <公开 类型 = 文本型>
    变量 路径 <公开 类型 = 文本型>
    变量 为目录 <公开 类型 = 逻辑型>

    # @begin
    # <> <include>
    # // 创建一个带参构造函数
    # @sn<current_class>(CVolString& time, CVolString& reason, CVolString& name, CVolString& path, BOOL isDir) {
    #     @<时间>.SetText(time);
    #     @<更改理由>.SetText(reason);
    #     @<文件名称>.SetText(name);
    #     @<路径>.SetText(path);
    #     @<为目录> = isDir;
    # }
    # <> </include>
    # @end
}

类 文件更改数组类 <公开 基础类 = PIV数组模板 注释 = "用于超级列表框的虚拟化数据" @模板实现类 = "文件更改日志">
{

    #
}
