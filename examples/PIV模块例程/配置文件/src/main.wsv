<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 298\"" 标题 = "配置文件读写">
{
    变量 按钮_创建 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"59, 13, 160, 36\"" 标题 = "创建配置项">
    变量 按钮_读 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"264, 13, 160, 36\"" 标题 = "读写配置项">
    变量 编辑框1 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"21, 59, 456, 224\"" 是否允许多行 = 真
            滚动条 = 纵向滚动条>
    变量 ""
    变量 配置文件 <类型 = 配置文件操作类>

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件">
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_创建)
        {
            创建配置项 ()

        }
        否则 (来源对象 == 按钮_读)
        {
            读写配置项 ()

        }
        返回 (0)
    }

    方法 创建配置项
    {
        // 配置文件默认是空的,可以直接置入值
        配置文件.置文本键值 ("按钮_创建", , , "; \"按钮_创建\"的配置项")  // 这一行是演示如何写"节"的注释,可以不用
        配置文件.置文本键值 ("按钮_创建", "标题", 按钮_创建.标题, "# 标题注释文本")
        配置文件.置整数键值 ("按钮_创建", "宽度", 按钮_创建.宽度)
        配置文件.置整数键值 ("按钮_创建", "高度", 按钮_创建.高度)
        配置文件.置整数键值 ("按钮_创建", "顶边", 按钮_创建.顶边)
        配置文件.置整数键值 ("按钮_创建", "左边", 按钮_创建.左边)
        配置文件.置逻辑键值 ("按钮_创建", "禁止", 按钮_创建.禁止)

        如果 (配置文件.保存文件 (取运行目录 () + "temp.ini", 文本编码类型.UTF8) >= 0)
        {
            信息框 ("创建配置文件成功:" + 取运行目录 () + "temp.ini")
        }
        否则
        {
            信息框 ("创建配置文件失败。")

        }
    }

    方法 读写配置项
    {
        // 使用"创建XXX"命令会覆盖之前的数据
        如果 (文件是否存在 (取运行目录 () + "temp.ini") == 假)
        {
            信息框 ("请先单击“创建”按钮。")
        }
        配置文件.创建自文件 (取运行目录 () + "temp.ini", 文本编码类型.UTF8, 真)  // "打开文件"跟这个一样

        变量 配置项信息 <类型 = 配置项信息数组类>
        配置文件.取所有键名2 ("按钮_创建", 配置项信息)
        调试输出 (配置项信息)

        // 修改配置项
        配置文件.置整数键值 ("按钮_创建", "宽度", 200)
        配置文件.置整数键值 ("按钮_创建", "高度", 40)
        配置文件.置整数键值 ("按钮_创建", "左边", 10)

        编辑框1.内容 = 配置文件.到文本 ()  // 将整个配置文件的内容放到编辑框中

        按钮_创建.标题 = 配置文件.取文本键值 ("按钮_创建", "标题")
        按钮_创建.宽度 = 配置文件.取整数键值 ("按钮_创建", "宽度")
        按钮_创建.高度 = 配置文件.取整数键值 ("按钮_创建", "高度")
        按钮_创建.左边 = 配置文件.取整数键值 ("按钮_创建", "左边")
        按钮_创建.禁止 = 配置文件.取逻辑键值 ("按钮_创建", "禁止")

        // 必须调用"保存"或"保存文件",才会将更改保存到ini文件中
        配置文件.保存 ()

    }
}
