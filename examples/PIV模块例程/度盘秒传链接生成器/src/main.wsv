<火山程序 类型 = "通常" 版本 = 1 />

包 火山.程序 <注释 = "本例子主要演示文件MD5" 注释 = "百度网盘秒传链接的介绍请看:" 注释 = "https://greasyfork.org/zh-CN/scripts/424574">

类 启动类 <公开 基础类 = 窗口程序类 折叠>
{
    变量 主窗口对象 <类型 = 我的主窗口>

    方法 启动方法 <公开 类型 = 整数>
    {
        主窗口对象.创建主窗口 ()
        返回 (1)
    }
}

# ===

类 我的主窗口 <基础类 = 窗口 注释 = "样例主窗口" @视窗.布局 = "client_size = \"500, 250\"" 标题 = "度盘秒传链接生成器 1.1">
{
    变量 按钮_生成链接 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 101\r\npos = \"269, 168, 100, 32\"" 标题 = "生成链接">
    变量 选择框_子目录 <类型 = 选择框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 102\r\npos = \"360, 123, 120, 24\"" 标题 = "包含子目录"
            选中 = 真>
    变量 进度条1 <类型 = 进度条 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 103\r\npos = \"20, 214, 460, 24\"" 最小位置 = 0 最大位置 = 100>
    变量 标签_来源 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 104\r\npos = \"20, 24, 80, 24\"" 标题 = "来源目录:">
    变量 编辑框_来源 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 105\r\npos = \"110, 20, 290, 32\"">
    变量 按钮_浏览 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 106\r\npos = \"407, 20, 80, 32\"" 标题 = "浏览">
    变量 标签_文件 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 107\r\npos = \"21, 73, 80, 24\"" 标题 = "链接文件:">
    变量 编辑框_链接 <类型 = 编辑框 隐藏值属性 = "0" @视窗.布局 = "id = 108\r\npos = \"110, 67, 290, 32\"">
    变量 按钮_浏览文件 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 109\r\npos = \"408, 68, 80, 32\"" 标题 = "浏览">
    变量 标签_文件数量 <类型 = 标签 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 110\r\npos = \"20, 123, 80, 24\"" 标题 = "文件数量:">
    变量 编辑框_文件数量 <类型 = 编辑框 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 111\r\npos = \"110, 119, 120, 32\"" 输入方式 = 只读方式>
    变量 按钮_停止 <类型 = 按钮 折叠2 隐藏值属性 = "0" @视窗.布局 = "id = 112\r\npos = \"384, 168, 100, 32\"" 标题 = "停止" 禁止 = 真>
    变量 标签_进度 <类型 = 标签 隐藏值属性 = "0" @视窗.布局 = "id = 113\r\npos = \"20, 173, 200, 24\"">
    变量 ""
    变量 目录名称 <类型 = 文本型>
    变量 文件数组 <类型 = 文本数组类>
    变量 枚举线程 <类型 = 线程类>
    变量 生成线程 <类型 = 线程类>

    方法 按钮_被单击 <接收事件 类型 = 整数 注释 = "当按钮被单击后发送此事件" 折叠>
    参数 来源对象 <类型 = 按钮 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 按钮_浏览)
        {
            目录名称 = 浏览文件夹 ("请选择来源目录")
            编辑框_来源.标题 = 目录名称
            如果 (文本是否为空 (编辑框_链接.标题))
            {
                编辑框_链接.标题 = 目录名称 + "\\baidulink.txt"
            }
            枚举线程.启动 ()
        }
        否则 (来源对象 == 按钮_浏览文件)
        {
            变量 文件名 <类型 = 文本型>
            如果 (通用文件对话框.保存文件 (文件名, "文本文件(*.txt)|*.txt", 本对象.取窗口句柄 (), , , 目录名称, ".txt"))
            {
                编辑框_链接.标题 = 文件名
            }
        }
        否则 (来源对象 == 按钮_生成链接)
        {
            如果 (文本是否为空 (编辑框_来源.标题))
            {
                信息框 ("请选择你要生成链接的来源目录。", 信息框按钮.警告图标)
                返回 (0)
            }
            如果 (文本是否为空 (编辑框_链接.标题))
            {
                信息框 ("请选择保存百度秒传链接的文件。", 信息框按钮.警告图标)
                返回 (0)
            }
            如果 (文本是否为空 (编辑框_文件数量.标题))
            {
                信息框 ("当前的文件数量为 0，或正在枚举文件。", 信息框按钮.警告图标)
                返回 (0)
            }
            生成线程.启动 ()

        }
        否则 (来源对象 == 按钮_停止)
        {
            生成线程.通知停止 ()
            按钮_停止.禁止 = 真

        }
        返回 (0)
    }

    方法 线程类_线程运行 <接收事件 类型 = 整数 注释 = "  用户需要接收并处理本事件以执行所需要的功能,该事件处理方法将在一个单独的线程中运行."
            注释 = "  注意: 在本事件的接收处理方法中,必须尽可能快地频繁调用本对象的\"是否需要退出\"方法,"
            注释 = "一旦发现该方法返回真,则需要尽快结束执行并返回,否则将导致另一线程中的\"停止\"方法调用" 注释 = "进入死等状态." 折叠>
    参数 来源对象 <类型 = 线程类 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        如果 (来源对象 == 枚举线程)
        {
            编辑框_文件数量.标题 = ""
            文件数组.重置为空对象 ()
            文件枚举类.枚举文件 (目录名称, "*.*", 文件数组, 真, 选择框_子目录.选中, 6)
            编辑框_文件数量.标题 = 到文本 (文件数组.取成员数 ())

        }
        如果 (来源对象 == 生成线程)
        {
            变量 文件操作 <类型 = 度盘链接生成类>
            变量 计时器 <类型 = 标准计时器类>
            如果 (文件操作.打开 (编辑框_链接.标题) == 假)
            {
                信息框 ("无法创建或打开下列文件，请检查是否有该路径的访问权限。\r\n" + 编辑框_链接.标题, 信息框按钮.警告图标)
                返回 (0)
            }
            按钮_生成链接.禁止 = 真
            按钮_停止.禁止 = 假
            变量 成员数 <类型 = 整数>
            成员数 = 文件数组.取成员数 ()
            文件数组.枚举循环 ()
            {
                标签_进度.标题 = 取格式文本 ("进度: %06d/%06d", 文件数组.取枚举索引 (), 成员数)
                如果 (生成线程.是否需要退出 (0) == 真)
                {
                    进度条1.位置 = 0
                    按钮_生成链接.禁止 = 假
                    返回 (0)
                }
                如果 (文件操作.生成度盘链接 (文件数组.取枚举值 (), 空对象, 选择框_子目录.选中, 目录名称) == 假)
                {
                    // 写报错

                }
                进度条1.位置 = (整数)((小数)文件数组.取枚举索引 () / (小数)成员数 * 100)

            }
            进度条1.位置 = 100
            文件操作.关闭 ()
            变量 毫秒 <类型 = 长整数>
            毫秒 = 计时器.取经历时间 (标准时间字段.毫秒)
            信息框 (取格式文本 ("生成百度秒传链接成功，请从文本文件中查看生成的链接。\r\n耗时: %I64d.%03I64d 毫秒", 毫秒 / 1000, 毫秒 % 1000), 信息框按钮.信息图标)
            按钮_生成链接.禁止 = 假
            按钮_停止.禁止 = 真

        }
        返回 (0)
    }

    方法 我的主窗口_将被销毁 <接收事件 类型 = 整数 注释 = "当本组件被销毁前发送此事件" 折叠>
    参数 来源对象 <类型 = 我的主窗口 注释 = "提供事件产生的具体来源对象">
    参数 标记值 <类型 = 整数 注释 = "用户调用\"挂接事件\"命令时所提供的\"标记值\"参数值,非此方式挂接事件则本参数值固定为0.">
    {
        枚举线程.通知停止 ()
        生成线程.通知停止 ()
        返回 (0)
    }
}

类 度盘链接生成类 <公开 折叠>
{
    变量 文件操作 <类型 = 文件读写类>
    变量 缓存文本 <类型 = 文本型>
    变量 累计尺寸 <类型 = 长整数>

    方法 类_初始化 <折叠>
    {
        置文本预分配字符数 (缓存文本, 8192)
    }

    方法 类_清理 <折叠>
    {
        关闭 ()
    }

    方法 打开 <公开 类型 = 逻辑型 折叠>
    参数 文件名 <类型 = 文本型>
    {
        如果 (文件操作.打开文件 (文件名, 文件打开方式.改写))
        {
            如果 (文件操作.取文件长度 () == 0)
            {
                文件操作.写出字节集 (创建字节集 (0xFF, 0xFE))
            }
            返回 (真)
        }
        否则
        {
            返回 (假)
        }
    }

    方法 关闭 <公开 折叠>
    {
        写出缓存 ()
        文件操作.关闭文件 ()
    }

    方法 生成度盘链接 <公开 类型 = 逻辑型 折叠>
    参数 文件名 <类型 = 文本型>
    参数 度盘链接 <类型 = 文本型 注释 = "成功时返回度盘链接" @默认值 = 空对象>
    参数 是否带路径 <类型 = 逻辑型 @默认值 = 假>
    参数 父路径 <类型 = 文本型 @默认值 = 空对象>
    {
        变量 文件大小 <类型 = 长整数>
        文件大小 = 取文件尺寸 (文件名)
        如果 (文件大小 == -1)
        {
            返回 (假)
        }
        变量 完整MD5 <类型 = 文本型>
        完整MD5 = MD5校验类.取文件MD5 (文件名)
        置文本预分配字符数 (度盘链接, 80 + 取文本长度 (文件名))
        度盘链接 = 完整MD5
        加入字符 (度盘链接, '#')
        如果 (文件大小 <= 256 * 1024)
        {
            加入文本 (度盘链接, 完整MD5)
        }
        否则
        {
            变量 数据 <类型 = 字节集类>
            如果 (数据.从文件读字节集 (文件名, 256 * 1024) == -1)
            {
                返回 (假)

            }
            加入文本 (度盘链接, MD5校验类.取数据MD5 (数据))

        }
        加入字符 (度盘链接, '#')
        加入长整数文本 (度盘链接, 文件大小)
        加入字符 (度盘链接, '#')
        变量 文件名称 <类型 = 文本型>
        如果 (是否带路径)
        {
            到相对路径文件名 (文件名, 父路径, 文件名称)
            子文本替换 (文件名称, "\\", "/")
            插入字符 (文件名称, 0, '/')
        }
        否则
        {
            文件名称 = 取文件名无路径部分 (文件名)

        }
        加入文本 (度盘链接, 文件名称)
        加入文本 (缓存文本, 度盘链接 + "\r\n")
        累计尺寸 = 累计尺寸 + 文件大小
        如果 (累计尺寸 > 1024 * 1024 * 512)  // 500MB
        {
            写出缓存 ()

        }
        返回 (真)
    }

    方法 写出缓存 <公开 折叠>
    {
        如果 (文件操作.写出文本 (缓存文本, 假) == 真)
        {
            清空文本 (缓存文本)
            累计尺寸 = 0

        }
    }
}
